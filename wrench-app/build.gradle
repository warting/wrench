apply plugin: 'com.android.application'

android {

    dataBinding {
        enabled = true
    }

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.izettle.wrench"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 11
        versionName "1.0.10"

        testInstrumentationRunner "com.izettle.wrench.WrenchTestRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        def configurationAuthorityValue = applicationId + ".configprovider"
        def wrenchPermission = applicationId + ".permission"

        manifestPlaceholders = [configurationAuthority: configurationAuthorityValue, wrenchPermission: wrenchPermission]

        buildConfigField "String", "CONFIG_AUTHORITY", "\"${configurationAuthorityValue}\""

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'

            versionNameSuffix " debug"
            applicationIdSuffix = ".debug"
        }
    }
    lintOptions {
        baseline file("lint-baseline.xml")
        checkReleaseBuilds true
        abortOnError true
        warningsAsErrors true
        lintConfig file("../lint.xml")
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    testImplementation deps.junit

    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.room.testing

    annotationProcessor deps.lifecycle.compiler
    annotationProcessor deps.room.compiler
    annotationProcessor deps.dagger.android_support_compiler
    annotationProcessor deps.dagger.compiler

    implementation deps.support.app_compat
    implementation deps.support.recyclerview
    implementation deps.support.design
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation deps.constraint_layout
    implementation deps.support.cardview
    implementation deps.lifecycle.extensions
    implementation deps.room.runtime
    implementation deps.paging

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support

    implementation "com.izettle.wrench:wrench-core:0.3"
    implementation "com.izettle.wrench:wrench-prefs:0.3"
}
